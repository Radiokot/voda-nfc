plugins {
    id 'com.android.application'
    id 'org.jetbrains.kotlin.android'
}

def keystorePropertiesFile = rootProject.file("app/keystore.properties")
def keystoreProperties = new Properties()
if (keystorePropertiesFile.exists()) {
    keystoreProperties.load(new FileInputStream(keystorePropertiesFile))
}

android {
    compileSdkVersion 33

    defaultConfig {
        applicationId "ua.com.radiokot.voda"
        minSdkVersion 19
        targetSdkVersion 33
        versionCode 3
        versionName "1.0.1"

        multiDexEnabled = true

        resConfigs("uk", "ru")

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"

        if (keystoreProperties['CardKeyHex'] != null) {
            buildConfigField 'String', 'CARD_KEY_HEX', "\"" + keystoreProperties['CardKeyHex'] + "\""
        } else {
            throw new GradleException("CardKeyHex is required to read card data")
        }

        buildConfigField 'Boolean', 'DUMMY_READER', 'false'
    }

    buildTypes {
        debugDummyReader {
            initWith buildTypes.debug

            buildConfigField 'Boolean', 'DUMMY_READER', 'true'
        }

        release {
            signingConfig signingConfigs.debug
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    kotlinOptions {
        jvmTarget = JavaVersion.VERSION_1_8.toString()
    }

    buildFeatures {
        viewBinding true
    }
}

dependencies {
    implementation fileTree(dir: "libs", include: ["*.jar"])
    implementation 'androidx.core:core-ktx:1.9.0'
    implementation 'androidx.appcompat:appcompat:1.6.1'
    implementation 'androidx.constraintlayout:constraintlayout:2.1.4'
    implementation 'androidx.viewpager2:viewpager2:1.0.0'
    implementation 'com.google.android.material:material:1.8.0'
    implementation 'androidx.multidex:multidex:2.0.1'

    implementation 'io.reactivex.rxjava2:rxkotlin:2.0.0'
    implementation 'io.reactivex.rxjava2:rxandroid:2.1.1'
    implementation "io.insert-koin:koin-core:$koin_version"
    implementation "io.insert-koin:koin-android:$koin_android_version"
    implementation 'me.grantland:autofittextview:0.2.1'

    testImplementation 'junit:junit:4.13.2'
    androidTestImplementation "io.insert-koin:koin-test:$koin_version"
    androidTestImplementation 'androidx.test.ext:junit:1.1.5'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.5.1'
}
